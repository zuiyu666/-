---
aliases: 
tags: 
width:
date created: 星期五, 十月 14日 2022, 4:12:32 下午
date modified: 星期二, 十月 18日 2022, 1:12:18 下午
---
#### 3.4.1.1 Applied Stress Gradient
*3.4.1.1外加应力梯度*
The block edge apply command may also take the keywords grad-x and grad-y, which allow the applied stresses or forces to vary linearly over the specified range. Three parameters follow each of these keywords and describe the variation of the stress components in either the x- or y-direction:
>块边缘应用命令也可以采用关键字grad-x和grad-y，这允许施加的应力或力在指定范围内线性变化。每个关键词后面都有三个参数，描述了应力分量在x或y方向上的变化：

```fish
grad-x sxxx sxyx syyx

grad-y sxxy sxyy syyy
```

The stresses vary linearly with distance from the global coordinate origin of x = 0, y = 0:
>应力随距离全局坐标原点x=0，y=0的距离线性变化：

$$\begin{aligned}
\sigma_{x x} &=\sigma_{x x}^{\circ}+(s x x x) x+(s x x y) y \\
\sigma_{x y} &=\sigma_{x y}^{\circ}+(s x y x) x+(s x y y) y \\
\sigma_{y y} &=\sigma_{y y}^{\circ}+(s y y x) x+(s y y y) y
\end{aligned} \tag {3.10}$$
where σ◦ xx , σ◦ xy and σ◦ yy are the stress components at the origin.
>其中σ◦ xx，σ◦ xy和σ◦ yy是原点处的应力分量。

The operation of this feature is best explained by an example:
>该功能的操作最好通过一个示例来解释：

```fish
block edge apply stress 0,0,-10e6 grad-y 0,0,1e5 range -.1,.1 -100,0
```

The stresses at the origin (x = 0, y = 0) are
>原点（x=0，y=0）处的应力为

$$\begin{array}{l}
\sigma_{x x}^{\circ}=0 \\
\sigma_{x y}^{\circ}=0 \\
\sigma_{y y}^{\circ}=-10 \times 10^{6}
\end{array}$$
The equation for the y-variation in stress component σyy is
>应力分量σyy的y变化方程为

$$\sigma_{y y}=-10 \times 10^{6}+\left(10^{5}\right) y$$
The value for σyy at y = −100 is then −20 × 106. At points in between, the y-variation is linearly scaled to the relative y-distance from the origin.
>y处σyy的值=−那么100就是−20 × 106. 在两者之间的点处，y变化按与原点的相对y距离线性缩放。

Typically, applied stress gradients are used to reproduce the effects of increasing stress with depth caused by gravity. It is important to make sure that the applied gradient is compatible with the gradient specified with the block insitu command and the value of gravitational acceleration (block mech gravity command). Section 3.5 provides more details on this matter.
>通常，应用的应力梯度用于再现重力引起的应力随深度增加的影响。务必确保应用的梯度与块就地命令和重力加速度值（块机械重力命令）指定的梯度兼容。**第3.5节**提供了有关该事项的更多详细信息。

#### 3.4.1.2 Changing Boundary Stresses
*变化的边界应力*

As previously discussed, transient loading can be performed with the history keyword for dynamic analysis. For static analysis, it may also be necessary to alter the values of applied stresses during the course of a UDEC simulation. For example, the load on a footing may change. To effect a sudden change in an existing applied stress or load, a new BOUNDARY command is given with the range that encompasses the same boundary corners as in the original command, but with a change in stress value or variation.
>如前所述，可以使用history关键字执行瞬态加载，以进行动态分析。对于静态分析，可能还需要在UDEC模拟过程中改变施加应力的值。例如，基脚上的荷载可能会发生变化。为了使现有施加的应力或载荷发生突变，将发出一个新的边界命令，其范围与原始命令中包含的边界角相同，但应力值或变化会发生变化。

In this case, the new value will be added to the existing value.* If the stress is to be removed, the current value should be given with an opposite sign. If a transient load is changed (i.e., a load assigned with the history keyword), any new load with the same history type is added to the existing load. However, a new transient load with a different history type replaces the old transient load.
>在这种情况下，新价值将添加到现有价值中。如果要消除应力，则应使用相反的符号给出当前值。如果瞬态负载发生变化（即，分配了历史关键字的负载），则具有相同历史类型的任何新负载都将添加到现有负载。然而，具有不同历史类型的新瞬态负载将取代旧的瞬态负载。

#### 3.4.1.3 Printing and Plotting
*3.4.1.3打印和绘图*

The boundary stresses and loads may be verified with the commands **block edge list** and **plot item create boundary**. The **block edge list** command lists the boundary corner addresses along with current values and conditions assigned to each corner. Once a **block edge apply** command is issued, a boundary corner list is created around the entire outer boundary of the model, regardless of the region affected by the block edge apply command. Optional keywords can be used with the block edge list command to check the different conditions along the boundary. For example,
>边界应力和载荷可通过命令block edge list和plot item create boundary进行验证。“块边缘列表”命令列出边界角点地址以及分配给每个角点的当前值和条件。发出块边缘应用命令后，将围绕模型的整个外部边界创建边界角点列表，而不管受块边缘应用指令影响的区域。可选关键字可与块边缘列表命令一起使用，以检查边界沿线的不同条件。例如

```fish
block edge list force
```

lists the permanent forces (fx,fy) and incremental forces (fxi,fyi) added during the current loading stage. If transient loads are applied (with the **block edge apply... hist** command), the total forces refer to the permanent plus transient loads at the current cycle number.
>列出了在当前加载阶段添加的永久力（fx，fy）和增量力（fxi，fyi）。如果施加瞬态载荷（使用块边缘应用…hist命令），则总力是指当前循环次数下的永久载荷加瞬态载荷。

#### 3.4.1.4 Caution and Advice
*3.4.1.4警告和建议*

In this section, some miscellaneous difficulties with stress boundaries are described. With UDEC, it is possible to apply stresses to the boundary of a body that has no displacement constraints (unlike many finite element programs, which require some constraints). The body will react in exactly the same way as a real body would (i.e., if the boundary stresses are not in equilibrium, then the whole body will start moving). Example 3.11 illustrates the effect:
>本节描述了应力边界的一些其他困难。使用UDEC，可以在没有位移约束的物体边界上施加应力（与许多需要一些约束的有限元程序不同）。物体的反应方式与真实物体完全相同（即，如果边界应力不平衡，则整个物体将开始移动）。示例3.11说明了这种效果：

**Example 3.11** Spin when model is not in equilibrium
*模型不平衡时的自旋*
```fish
model new
block tolerance corner-round-length 0.01
block create polygon 0 0 -3 9.54 6.54 12.54 9.54 3
block zone gen quad 2.0
block zone group ’block’
block zone cmodel assign elastic density 2.4E3 bulk 8E9 shear 5E9 ...
range group ’block’
block edge apply stress -2000000.0 0.0 0.0 ...
range pos-x -3.1 0.1 pos-y -0.1 9.55
block edge apply stress -2000000.0 0.0 0.0 ...
range pos-x 6.5 9.55 pos-y 2.9 12.6
;
block cycle 500
model save ’ex3_11.sav’
```

The plot produced is given in Figure 3.20. The applied σ11 causes horizontal forces to act on the body. Because the body is tilted, these forces give rise to a moment that causes the body to spin.
>图3.20给出了产生的曲线图。施加的σ11使水平力作用在物体上。因为身体是倾斜的，这些力会产生一个力矩，使整体旋转。

![](https://obsidianxjb.oss-cn-hangzhou.aliyuncs.com/obsidian/20220731093514.png)

A similar, but more subtle, effect arises when material is excavated from a body that is supported by a stress boundary condition: the body is initially in equilibrium under gravity, but the removal of material reduces the weight. The whole body then starts moving upward, as demonstrated in Example 3.12:
>当从受应力边界条件支撑的物体中挖掘材料时，会产生类似但更微妙的影响：物体在重力作用下最初处于平衡状态，但移除材料会减轻重量。然后，整个身体开始向上移动，如例3.12所示：

**Example 3.12** Uplift when material is removed
*移除材料时的提升*

```fish
model new

block tolerance corner-round-length 0.01

block create polygon 0 0 0 10 10 10 10 0

block cut crack 0 5 10 5

block zone gen quad 1.0

block zone group ’block’

block zone cmodel assign elastic density 1E3 bulk 8E9 ...

shear 5E9 range group ’block’

block contact group ’joint’

block contact cmodel assign area stiffness-shear 1E10 ...

stiffness-normal 1E10 range group ’joint’

; new contact default

block contact cmodel default area stiffness-shear 1E10 ...

stiffness-normal 1E10

model gravity 0.0 -10.0

block edge apply stress 0.0 0.0 -100000.0 range pos-y -0.1 0.1
block gridpoint apply velocity-x 0 range pos-x -0.1 0.1

block gridpoint apply velocity-x 0 range pos-x 9.9 10.1

block insitu stress 0.0 0.0 -100000.0 gradient-x 0.0 0.0 0.0 ...

gradient-y 0.0 0.0 10000.0

block gridpoint history displacement-y 5.0 2.5

block cycle 300

block zone group ’Null:excavate’ range atblock 5 7.5

block zone cmodel assign null range group ’Null:excavate’

block cycle 100
```

The difficulty encountered in running this data file can be eliminated by fixing the bottom boundary, rather than supporting it with stresses. Section 3.4.4 contains information relating to the location of such artificial boundaries.
>运行此数据文件时遇到的困难可以通过固定底部边界来消除，而不是用应力来支撑它。第3.4.4节包含与此类人工边界位置相关的信息。

Finally, the stress boundary affects all degrees of freedom. Velocity boundary conditions must, therefore, be prescribed after stress boundary conditions affecting the same boundary corners. If the stress boundary is applied after the velocity boundary, the effect of the prescribed velocity will be lost. Example 3.13 demonstrates this problem:
>最后，应力边界影响所有自由度。因此，必须在影响相同边界角的应力边界条件之后规定速度边界条件。如果在速度边界之后应用应力边界，则会失去规定速度的影响。示例3.13演示了这个问题：

**Example 3.13** Mixing stress and velocity boundary conditions
*13混合应力和速度边界条件*
```fish
model new

block tolerance corner-round-length 0.01

block create polygon 0,0 0,10 10,10 10,0

block zone gen quad 2.0

block zone group ’block’

block zone cmodel assign elastic density 1E3 bulk 8E9 shear 5E9 ...

range group ’block’

bl grid app velocity-y 0 range pos-y -0.1,0.1

bl edg app stress -100000.0,0.0,0.0 range pos-x -0.1,0.1

bl edg app stress -100000.0,0.0,0.0 range pos-x 9.9,10.1

bl edg app stress 0.0,0.0,-200000.0 range pos-y 9.9,10.1

block grid history disp-y 0.0,0.0

block cycle 100

ret
```

The fixed y-velocity boundary condition along the bottom boundary of the model is removed at the right and left corners when the stress boundaries are applied. These points move downward, as indicated by the history plot, when the model is loaded.
>当应用应力边界时，沿模型底部边界的固定y速度边界条件在左右角被移除。加载模型时，这些点向下移动，如历史图所示。

### 3.4.3 Real Boundaries – Choosing the Right Type
*真正的界限——选择正确的类型*
It is sometimes difficult to know which type of boundary condition to apply to a particular surface on the body being modeled. For example, in modeling a laboratory triaxial test, should the load applied by the platen be regarded as a stress boundary, or should the platen be treated as a rigid, displacement boundary? Of course, the whole testing machine, including the platen, could be modeled, but that might be very time-consuming. Remember that UDEC takes a long time to converge if there is a large contrast in stiffnesses. In general, if the object applying the load is very stiff compared with the sample (say, more than 20 times stiffer), then it may be treated as a rigid boundary. If it is soft compared with the sample (say, 20 times softer), then it may be modeled as a stress-controlled boundary. Clearly, a fluid pressure acting on the surface of a body is in the latter category. Footings on jointed rock can often be represented as rigid boundaries that move with constant velocity for the purposes of finding the collapse load of the rock. This approach has another advantage: it is much easier to control the test and obtain a good load/displacement graph.
It is well-known that stiff testing machines are more stable than soft testing machines.
>有时很难知道哪一种边界条件适用于被建模的物体的特定表面。
例如，在模拟实验室三轴试验时，应将压板施加的载荷视为应力边界，还是应将压板视为刚性的位移边界?
当然，整个测试机器，包括压板，都可以建模，但这可能非常耗时。
记住，如果在刚度上有很大的对比，UDEC需要很长的时间来收敛。
一般来说，如果施加载荷的物体与样品相比非常坚硬(比方说，比样品坚硬20倍以上)，那么它可以被视为刚性边界。
如果它与样品相比是软的(比方说，软20倍)，那么它可以被建模为应力控制的边界。
显然，作用于物体表面的流体压力属于后一类。
在节理岩石上的基础通常可以用恒定速度移动的刚性边界来表示，以求得岩石的崩塌荷载。
这种方法还有另一个优点:它更容易控制试验，并获得良好的载荷/位移图。
众所周知，刚性测试机比软测试机更稳定。

### 3.4.4 Artifificial Boundaries
*3.4.4 人工边界*
Artificial boundaries fall into two categories: lines of symmetry and lines of truncation.
>人工边界分为两类:对称线和截断线。

#### 3.4.4.1 Symmetry Lines
*3.4.4.1对称线*
Sometimes it is possible to take advantage of the fact that the geometry and loading in a system are symmetrical about one or more lines. For example, if everything is symmetrical about a vertical line, then the horizontal displacements on that line will be zero. Therefore, we can make that line a boundary and fix all gridpoints in the x-direction, using the block grid apply vel-x = 0 command. If velocities on the line of symmetry are not already zero, they will be set to zero with this command.
In the case considered, the y-component of velocity on the vertical line of symmetry is not affected; it should not be fixed. Similar considerations apply to a horizontal line of symmetry. The block grid apply vel-n = 0 command can be used to set lines of symmetry that lie at angles to the coordinate axes.
>有时候我们可以利用系统中的几何图形和加载是关于一条或多条线对称的这一事实。
例如，如果所有的东西都是关于一条垂直线对称的，那么这条直线上的水平位移将为零。
因此，我们可以将这条线作为边界，并使用block grid apply vell -x = 0命令将所有网格点固定在x方向上。
如果在对称线上的速度不是零，它们将被这个命令设置为零。
在这种情况下，速度在垂直对称线上的y分量不受影响;
它不应该被修复。
类似的考虑也适用于水平对称线。
块网格应用vel-n = 0命令可以用于设置与坐标轴成角度的对称线。

As discussed in Section 3.2.3, the presence of discontinuities makes the application of symmetry lines more difficult. When using symmetry lines in UDEC, the modeler should always be careful to consider the effect of joint orientation.
>如3.2.3节所述，不连续的存在使对称线的应用更加困难。
当在UDEC中使用对称线时，建模者应该总是小心地考虑关节方向的影响。

3.4.4.2 Boundary Truncation